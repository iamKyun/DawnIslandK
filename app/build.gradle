/*
 *  Copyright 2020 Fishballzzz
 *  *
 *  * Licensed under the Apache License, Version 2.0 (the "License");
 *  * you may not use this file except in compliance with the License.
 *  * You may obtain a copy of the License at
 *  *
 *  *     http://www.apache.org/licenses/LICENSE-2.0
 *  *
 *  * Unless required by applicable law or agreed to in writing, software
 *  * distributed under the License is distributed on an "AS IS" BASIS,
 *  * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  * See the License for the specific language governing permissions and
 *  * limitations under the License.
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    signingConfigs {
        release {
            if (project.rootProject.file("local.properties").exists()) {
                Properties properties = new Properties()
                properties.load(project.rootProject.file("local.properties").newDataInputStream())
                def store_file = properties.getProperty("storeFile")
                def store_password = properties.getProperty("storePassword")
                def key_alias = properties.getProperty("keyAlias")
                def key_password = properties.getProperty("keyPassword")

                storeFile file(store_file)
                storePassword store_password
                keyAlias key_alias
                keyPassword key_password
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    android.ndkVersion "21.1.6352462"

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "com.laotoua.dawnislandk"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 19
        versionName "v1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildTypes {
        debug {
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            FirebasePerformance {
                // Set this flag to 'false' to disable @AddTrace annotation processing and
                // automatic HTTP/S network request monitoring
                // for a specific build variant at compile time.
                instrumentationEnabled false
            }
        }

        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            firebaseCrashlytics {
                mappingFileUploadEnabled true
            }
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.tencent:mmkv-static:1.1.1'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation "androidx.drawerlayout:drawerlayout:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.activity:activity-ktx:1.2.0-alpha07"
    implementation "androidx.fragment:fragment-ktx:1.3.0-alpha07"

    def lifecycle_version = "2.3.0-alpha06"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation 'com.google.android.material:material:1.3.0-alpha02'

    def appcompat_version = "1.3.0-alpha01"
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta8"

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.squareup.okhttp3:okhttp:4.7.2"
    implementation "com.squareup.moshi:moshi-kotlin:1.9.2"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.9.2"

    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.apache.commons:commons-text:1.8'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'

    implementation 'com.lxj:xpopup:2.0.8-rc5'
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation 'com.afollestad.material-dialogs:color:3.2.1'
    implementation 'com.afollestad.material-dialogs:datetime:3.2.1'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.2.1'

    def srl_version = "1.7.1.3.androidx"
    implementation "me.dkzwm.widget.srl:core:$srl_version"
    implementation "me.dkzwm.widget.srl:ext-util:$srl_version"
    implementation "me.dkzwm.widget.srl:ext-material:$srl_version"

    implementation 'com.king.zxing:zxing-lite:1.1.9-androidx'

    implementation 'com.github.zhpanvip:viewpagerindicator:1.0.4'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'id.zelory:compressor:3.0.0'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.19'

    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    def nav_version = "2.3.0"
    // Kotlin navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def dagger_version = "2.27"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
//    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    implementation 'com.google.firebase:firebase-analytics-ktx:17.4.4'
    implementation 'com.google.firebase:firebase-crashlytics:17.1.1'
    implementation 'com.google.firebase:firebase-inappmessaging-display-ktx:19.1.0'
    implementation 'com.google.firebase:firebase-config-ktx:19.2.0'
    implementation 'com.google.firebase:firebase-perf:19.0.8'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.3'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
}
